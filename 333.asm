DSEG SEGMENT;数据段
	NUM DW ?
	INPUTBUFF DB 0F0H
			  DB ?
			  DB 0F0H DUP(?)
	SUM DW ?
	TIP1 DB 'Please input total num:$'
	TIP2 DB 'Please input a number:$'
	TIP3 DB 'The sum is:$'
	NUMIDX	DW 1,10,100,1000,10000
DSEG  ENDS

ESEG  SEGMENT;附加段
   STRING2 DB 5 DUP(?)
ESEG  ENDS

SSEG SEGMENT;堆栈段
   DW 64 DUP(?)
SSEG  ENDS

CSEG  SEGMENT;代码段
		ASSUME  CS:CSEG,DS:DSEG;声明各段与段寄存器的关系
		ASSUME  ES:ESEG,SS:SSEG

SHOW	MACRO TEXTBUFF
		PUSH AX
		PUSH DX
		LEA DX,TEXTBUFF
		MOV AH,9H
		INT 21H
		POP DX
		POP AX
ENDM

GETNUM	PROC FAR ;此函数用于从键盘获取一个0~65535的数，并存储到AX中
		XOR AX,AX
		PUSH CX
		PUSH BX
		PUSH DX
		PUSH SI
		LEA DX,INPUTBUFF
		MOV AH,0AH
		INT 21H
		XOR CX,CX
		MOV CL,DS:[INPUTBUFF+1]
		MOV BX,2 ;从偏移为2开始
		XOR DX,DX
ASC2INT:	XOR AX,AX
			MOV AL,DS:[INPUTBUFF+BX]
			SUB AX,30H
			MOV SI,CX
			DEC SI
			ADD SI,SI
			PUSH DX
			MUL WORD PTR DS:[NUMIDX+SI]
			POP DX
			ADD DX,AX
			INC BX
			LOOP ASC2INT
		MOV AX,DX
		PUSH AX
		MOV DL,0AH
		MOV AH,02H
		INT 21H
		POP AX
		POP SI
		POP DX
		POP BX
		POP CX
		RET
GETNUM ENDP

SHOWNUM	PROC FAR ;此函数用于向屏幕输出一个0~65535的十进制数AX
		PUSH CX
		PUSH BX
		PUSH DX
		PUSH SI
		XOR DX,DX
		MOV BX,AX
		MOV CX,5H
INT2ASC:	MOV SI,CX
			DEC SI
			ADD SI,SI
			MOV AX,BX
			DIV WORD PTR DS:[NUMIDX+SI]
			PUSH AX
			XOR DX,DX
			MOV DL,AL
			ADD DL,30H
			MOV AH,02H
			INT 21H
			POP AX
			MUL WORD PTR DS:[NUMIDX+SI]
			SUB BX,AX
			LOOP INT2ASC
		XOR AX,AX
		POP SI
		POP DX
		POP BX
		POP CX
		RET
SHOWNUM ENDP

START:	MOV  AX,DSEG;设定数据段
		MOV  DS,AX
		MOV  AX,ESEG;设定附加段
		MOV  ES,AX
		MOV  AX,SSEG;设定堆栈段
		MOV  SS,AX
		
		SHOW TIP1
		CALL GETNUM
		MOV CX,AX
		MOV DS:[SUM],0
MAINLOP:	SHOW TIP2
			CALL GETNUM
			ADD DS:[SUM],AX
			LOOP MAINLOP
		SHOW TIP3
		MOV AX,DS:[SUM]
		CALL SHOWNUM
		
		MOV  AH,4CH;程序结束
		INT  21H
CSEG ENDS
END START